valuetype Verkehrsallowed oftype text {
    constraints: [ Verkehrstype ];
}
constraint Verkehrstype oftype AllowlistConstraint {
    allowlist: [ "FV", "RV", "nur DPN" ];
}

valuetype Coordinate oftype decimal {
    constraints: [ CoordinateBound ];
}
constraint CoordinateBound oftype RangeConstraint {
    lowerBound: -90;
    lowerBoundInclusive: true;
    upperBound: 90;
    upperBoundInclusive: true;
}

valuetype IFOPTValues oftype text {
    constraints: [ IFOPTFormat ];
}
constraint IFOPTFormat oftype RegexConstraint {
    regex: /[a-z]{2}:\d+:\d+(:\d+)?(:\d+)?/;
}
valuetype DeleteEmptyCell oftype text {
    constraints: [ DeleteEmpty ];
}
constraint DeleteEmpty oftype DenylistConstraint {
    denylist: [ "" ];
}

pipeline TrainStopsPipeline {
    TrainStopsExtractor
         -> TrainStopsDataInterpreter
         -> StopsCSVInterpreter
         -> StatusColumnDeleter
         -> TrainstopsTableInterpreter
         -> TrainStopsLoader;


    block TrainStopsExtractor oftype HttpExtractor {
        url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";

    }
    block TrainStopsDataInterpreter oftype TextFileInterpreter { }

    block StopsCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block StatusColumnDeleter oftype ColumnDeleter {
        delete: [column J];
    }

    block TrainstopsTableInterpreter oftype TableInterpreter {
        header: true;
 columns: [
    "EVA_NR" oftype integer,
    "DS100" oftype text,
    "IFOPT" oftype IFOPTValues,
    "NAME" oftype DeleteEmptyCell,
    "Verkehr" oftype Verkehrsallowed,
    "Laenge" oftype Coordinate,
    "Breite" oftype Coordinate,
    "Betreiber_Name" oftype DeleteEmptyCell,
    "Betreiber_Nr" oftype integer,
 ]; 
    
    }

    block TrainStopsLoader oftype SQLiteLoader {
        table: "trainstops";
        file: "./trainstops.sqlite";
    }

}





